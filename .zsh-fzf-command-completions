# create a simpler and lighter directory fzf completion
_fzf_complete_path() {
	local lbuf="$1"
	local prefix="$2"
	local original_prefix="${lbuf}$2"
	local mode="$3"  # string - `files` or `directories`
	local path_prefix
	if [[ "${prefix}" =~ "/" ]]; then
		path_prefix="${prefix%/*}/"
		prefix="${prefix##*/}"
	else
		path_prefix="./"
	fi
	# substiture ~ to $HOME in path_prefix
	path_prefix="${path_prefix/#\~/$HOME}"
	# remove \ when it is intended to quote special characters
	path_prefix="${path_prefix//\\/}"
	local query="${prefix//*\//}"
	local opts real_opts
	if [[ "$mode" == "directories" ]]; then
		opts=("${path_prefix}"*(-/N))
		if [ ! -z "$opts" ]; then
			real_opts=("${opts[@]/%//}")
		fi
	elif [[ "$mode" == "files" ]]; then
		opts=("${path_prefix}"*(N))
		local opt
		for opt in "${opts[@]}"; do
			if [[ -d "${opt}" ]]; then
				real_opts+=("${opt}/")
			else
				real_opts+=("${opt}")
			fi
		done
	fi
	if [ -z "$real_opts" ]; then
		zle reset-prompt
		return 0
	fi
	local matches=(${(f)"$(printf '%s\n' "${real_opts[@]}" | fzf --query="${query}" -m --preview='[[ -d {} ]] && ls --color {} || ~/.config/ranger/scope.sh {} $COLUMNS $LINES "" False')"})
	if [ -z "$matches" ]; then
		zle reset-prompt
		return 0
	fi
	LBUFFER="$lbuf${(@q)matches}"
	zle reset-prompt
}

# make sure cd is completed with the default fzf-completion widget with _fzf_complete_dir
_fzf_complete_cd() {
	_fzf_complete_path "$@" "directories"
}

# gopass and pass passwords
_fzf_complete_gopass() {
	local prefix="$2"
	local match="$(find -L ~/.password-store -name '.git*' -prune -o -type f -printf '%P\n' | sed 's/.gpg$//' | fzf --query="${prefix}")"
	[ -z "$match" ] && return 1
	LBUFFER="$lbuf${match}"
	zle reset-prompt
}
_fzf_complete_pass() {
	_fzf_complete_gopass "$@"
}

# export (zsh's built-in)
_fzf_complete_export() {
	local lbuf="$1"
	local prefix="$2"
	local matches=(${(f)"$(declare -x | fzf --query="${prefix}" --preview='eval echo \${}')"})
	[ -z "$matches" ] && return 1
	LBUFFER="$lbuf${matches[@]//=*/}"
}

# unset (zsh's built-in)
_fzf_complete_unset() {
	_fzf_complete_export "$@"
}

# unalias (zsh's built-in)
_fzf_complete_unalias() {
	local lbuf="$1"
	local prefix="$2"
	local aliases=(${(f)"$(alias)"})
	local matches=($(printf '%s\n' ${aliases[@]//=*/} | fzf --query="${prefix}"))
	[ -z "$matches" ] && return 1
	LBUFFER="$lbuf${matches[@]//=*/}"
}

# kill
_fzf_complete_kill() {
	local lbuf="$1"
	local prefix="$2"
	local matches=(${(f)"$(command ps -ef | sed 1d | fzf -m -q "$prefix" --height "50%" --min-height 15 --reverse --preview 'echo {}' --preview-window down:3:wrap)"})
	[ -z "$matches" ] && return 1
	local match_line line_parts pids
	for match_line in ${matches[@]}; do
		line_parts=(${(s: :)match_line})
		pids+=(${line_parts[2]})
	done
	LBUFFER="$lbuf${pids[@]}"
	zle reset-prompt
}

# juf and jsf shell functions
_fzf_complete_journal(){
	local lbuf="$1"
	local prefix="$2"
	local mode="$3"  # string - `user` or `system`
	local systemctl_cmd
	if [[ "$mode" == "user" ]]; then
		systemctl_cmd='systemctl --user'
	else
		systemctl_cmd='systemctl'
	fi
	local unit load active sub description
	local units
	local first_iteration=1 break_larger_loop=0
	eval $systemctl_cmd list-units --plain --all | while read unit load active sub description; do
		if (($first_iteration)); then
			first_iteration=0
			continue
		fi
		for field in "$unit" "$load" "$active" "$sub" "$description"; do
			if [[ ! -z "$field" ]]; then
				units+=("$unit")
				break
			else
				if [[ "$description" == "$field" ]]; then
					break_larger_loop=1
				fi
			fi
		done
		if (($break_larger_loop)); then
			break
		fi
	done
	local matches=($(printf '%s\n' "${units[@]}" | fzf -m --query="${prefix}" --preview="$systemctl_cmd status {}"))
	[ -z "$matches" ] && return 1
	LBUFFER="$lbuf${matches[@]}"
}
_fzf_complete_juf(){
	_fzf_complete_journal "$@" user
}
_fzf_complete_jsf(){
	_fzf_complete_journal "$@" system
}

# man pages
_fzf_complete_man(){
	local lbuf="$1"
	local prefix="$2"
	local name section dash description
	local matches=($(man -k . | fzf -m --query="${prefix}" | while read -r name section dash description; do
		echo "$name.${${section#\(}%\)}"
	done))
	[ -z "$matches" ] && return 1
	LBUFFER="$lbuf${matches[@]}"
}

# taskwarrior
_fzf_complete_task(){
	local lbuf="$1"
	local prefix="$2"
	if [[ "$prefix" =~ "^[-+]" ]]; then
		# complete tags
		local matches=($(command task _tags | fzf -m --query="${${prefix#+}#-}"))
		[ -z "$matches" ] && return 1
		local common_prefix="${prefix//[^-+]*/}"
		LBUFFER="$lbuf${matches[@]/#/${common_prefix}}"
		zle reset-prompt
		return 0
	elif [[ "$prefix" =~ "^project:" ]]; then
		# complete project
		local match=$(command task _project | fzf --query="${prefix#project:}")
		[ -z "$match" ] && return 1
		local common_prefix="project:"
		LBUFFER="$lbuf${match/#/${common_prefix}}"
		zle reset-prompt
		return 0
	else
		# complete task
		local matches=(${(f)"$(eval $lbuf _zshids | fzf -m --query="${prefix}")"})
		[ -z "$matches" ] && return 1
		LBUFFER="$lbuf${matches[@]//:*/}"
		zle reset-prompt
		return 0
	fi
}

_fzf_complete_t(){
	_fzf_complete_task "$@"
}
